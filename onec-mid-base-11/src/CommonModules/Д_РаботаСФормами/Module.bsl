#Область ПрограммныйИнтерфейс

//Добавляет на форму реквизиты и команды
//Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо разместить команды.
Процедура ДополнитьФорму(Форма) Экспорт
	
	// {{Молдованова В.Е.
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПолеСогласованнаяСкидкаИКомандуПересчитатьСумму(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);	
	КонецЕсли;
	// Молдованова В.Е.}}
	
КонецПроцедуры 

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма) 
	
	// {{Молдованова В.Е.
    ПолеКонтактноеЛицо = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеКонтактноеЛицо.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	// Молдованова В.Е.}}
	
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма) 
	
	// {{Молдованова В.Е.
	ПолеКонтактноеЛицо = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеКонтактноеЛицо.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	// Молдованова В.Е.}}
	
КонецПроцедуры

Процедура ДобавитьПолеСогласованнаяСкидкаИКомандуПересчитатьСумму(Форма)
	
	// {{Молдованова В.Е.
	Группа = Форма.Элементы.Добавить("ГруппаДоработки", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеСогласованнаяСкидка = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаДоработки);
	ПолеСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСогласованнаяСкидка.ПутьКДанным = "Объект.Д_СогласованнаяСкидка";
	ПолеСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "Подключаемый_Д_СогласованнаяСкидкаПриИзменении");
	
	КомандаПересчитатьСумму = Форма.Команды.Добавить("КомандаПересчитатьСумму"); 
	КомандаПересчитатьСумму.Заголовок = "Пересчитать сумму";
	КомандаПересчитатьСумму.Действие = "ПересчитатьСумму";
	
	ЭлементПересчитатьСумму = Форма.Элементы.Добавить("КомандаПересчитатьСумму", Тип("КнопкаФормы"), Форма.Элементы.ГруппаДоработки);
	ЭлементПересчитатьСумму.ИмяКоманды = "КомандаПересчитатьСумму";
	ЭлементПересчитатьСумму.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
	ЭлементПересчитатьСумму.Картинка = БиблиотекаКартинок.Перечитать; 
	ЭлементПересчитатьСумму.Отображение = ОтображениеКнопки.КартинкаИТекст;
	// Молдованова В.Е.}}
	
КонецПроцедуры

#КонецОбласти